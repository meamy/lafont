-- *******************************************************************
-- Bifunctoriality With Swap Gates
-- *******************************************************************

@name bifunct_X_SWAP_a
X0.SWAP12
expand_SWAP12 → 1
bifunct_X_CX_a ← 0
!apply bifunct_X_CX_b ← 1
bifunct_X_CX_a ← 2
expand_SWAP12 ← 0
SWAP12.X0

@name bifunct_X_SWAP_b
X2.SWAP01
expand_SWAP01 → 1
bifunct_X_CX_d ← 0
!apply bifunct_X_CX_c ← 1
bifunct_X_CX_d ← 2
expand_SWAP01 ← 0
SWAP01.X2

@name bifunct_Z_SWAP_a
Z0.SWAP12
expand_SWAP12 → 1
bifunct_Z_CX_b → 0
!apply bifunct_Z_CX_d → 1
bifunct_Z_CX_b → 2
expand_SWAP12 ← 0
SWAP12.Z0

@name bifunct_Z_SWAP_b
Z1.SWAP02
!apply expand_SWAP02_alt1 → 1
bifunct_Z_CX_c → 0
bifunct_Z_CX_a → 1
bifunct_Z_CX_c → 2
!apply expand_SWAP02_alt1 ← 0
SWAP02.Z1

@name bifunct_Z_SWAP_c
Z2.SWAP01
expand_SWAP01 → 1
!apply bifunct_Z_CX_e → 0
!apply bifunct_Z_CX_f → 1
!apply bifunct_Z_CX_e → 2
expand_SWAP01 ← 0
SWAP01.Z2


-- *******************************************************************
-- Bifunctoriality Between Pauli-X Gates
-- *******************************************************************

@name bifunct_X_b
X0.X2
!apply symmetry_X_c → 1
!apply bifunct_X_SWAP_a → 0
bifunct_X_a → 1
!apply bifunct_X_SWAP_a → 2
!apply symmetry_X_c ← 0
X2.X0

@name bifunct_X_c
X1.X2
expand_X1 → 0
!apply bifunct_X_SWAP_b ← 2
!apply bifunct_X_b → 1
!apply bifunct_X_SWAP_b ← 0
expand_X1 ← 1
X2.X1


-- *******************************************************************
-- Bifunctoriality Between Pauli-X Gates and Paul-Z Gates
-- *******************************************************************

@name bifunct_X_Z_a
X0.Z1
elim_K12 ← 1
bifunct_X_K_a → 0
r25_a ← 2
bifunct_X_a → 1
bifunct_X_K_a → 2
r25_a → 1
elim_K12 → 0
Z1.X0

@name bifunct_X_Z_b
X1.Z0
expand_Z0 → 1
!apply drel59 → 0
!apply bifunct_X_Z_a ← 1
!apply drel58 → 2
expand_Z0 ← 0
Z0.X1

@name bifunct_X_Z_c
X2.Z1
expand_Z1 → 1
!apply bifunct_X_SWAP_b → 0
!apply bifunct_X_Z_f → 1
!apply bifunct_X_SWAP_b → 2
expand_Z1 ← 0
Z1.X2

@name bifunct_X_Z_d
X1.Z2
elim_K12 ← 0
r25_a_dag → 1
r25_b ← 2
!apply bifunct_X_Z_c ← 1
r25_b_dag → 0
r25_a ← 1
elim_K12 → 2
Z2.X1

@name bifunct_X_Z_e
X0.Z2
expand_Z2 → 1
!apply elim_SWAP02 ← 0
expand_X2 ← 1
!apply bifunct_X_Z_f → 1
expand_X2 → 2
expand_Z2 ← 0
!apply elim_SWAP02 → 2
Z2.X0

@name bifunct_X_Z_f
X2.Z0
expand_Z0 → 1
bifunct_X_K_b → 0
!apply bifunct_X_b ← 1
bifunct_X_K_b → 2
expand_Z0 ← 0
Z0.X2


-- *******************************************************************
-- Bifunctoriality Between Pauli-X Gates and CX Gates
-- *******************************************************************

@name bifunct_X_CX_b
CX21.X0
expand_CX21 → 0
bifunct_X_K_a ← 2
bifunct_X_CX_a → 1
bifunct_X_K_a ← 0
expand_CX21 ← 1
X0.CX21

@name bifunct_X_CX_c
CX10.X2
expand_CX10 → 0
bifunct_X_K_b ← 2
bifunct_X_CX_d → 1
bifunct_X_K_b ← 0
expand_CX10 ← 1
X2.CX10

@name bifunct_X_CX_e
CX02.X1
expand_CX02 → 0
!apply elim_SWAP12 ← 4
!apply symmetry_X_c ← 2
bifunct_X_CX_d → 1
!apply symmetry_X_c → 1
!apply elim_SWAP12 → 0
expand_CX02 ← 1
X1.CX02


-- *******************************************************************
-- Bifunctoriality Between Pauli-X Gates and CZ Gates
-- *******************************************************************

@name bifunct_X_CZ_a
X1.CZ02
expand_CZ02 → 1
!apply elim_SWAP12 ← 0
!apply symmetry_X_c ← 1
expand_CZ01 → 2
r25_b → 1
!apply bifunct_Z_CX_e → 2
r25_b_dag ← 3
!apply symmetry_X_c → 4
!apply elim_SWAP12 → 6
expand_CZ01 ← 1
expand_CZ02 ← 0
CZ02.X1

@name bifunct_X_CZ_b
X0.CZ12
expand_CZ12 → 1
!apply elim_SWAP01 ← 0
expand_X1 ← 1
!apply bifunct_X_CZ_a → 1
expand_X1 → 2
!apply elim_SWAP01 → 4
expand_CZ12 ← 0
CZ12.X0


-- *******************************************************************
-- Bifunctoriality Between Pauli-Z Gates
-- *******************************************************************

@name bifunct_Z_a
Z0.Z1
expand_Z0 → 0
!apply drel59 ← 2
bifunct_X_a → 1
!apply drel58 ← 0
expand_Z0 ← 1
Z1.Z0

@name bifunct_Z_b
Z0.Z2
expand_Z0 → 0
!apply bifunct_Z_K_b ← 2
!apply bifunct_X_Z_e → 1
!apply bifunct_Z_K_b ← 0
expand_Z0 ← 1
Z2.Z0

@name bifunct_Z_c
Z1.Z2
elim_K12 ← 0
r25_a ← 1
r25_b ← 2
!apply bifunct_X_c → 1
r25_a → 2
r25_b → 1
elim_K12 → 0
Z2.Z1


-- *******************************************************************
-- Bifunctoriality Between Pauli-Z and CX Gates
-- *******************************************************************

@name bifunct_Z_CX_d
Z0.CX21
elim_K12 ← 1
!apply drel46 → 2
!apply bifunct_Z_K_a → 0
bifunct_Z_CX_b → 1
!apply bifunct_Z_K_a → 2
!apply drel46 ← 1
elim_K12 → 0
CX21.Z0

@name bifunct_Z_CX_e
Z2.CX01
elim_K01 ← 2
elim_K01 ← 1
expand_CX10 ← 2
!apply bifunct_Z_K_b → 0
expand_Z2 → 1
!apply elim_SWAP02 ← 5
!apply symmetry_CX_f ← 3
bifunct_Z_CX_b → 2
!apply symmetry_CX_f → 2
!apply elim_SWAP02 → 1
expand_Z2 ← 2
expand_CX10 → 1
elim_K01 → 0
!apply bifunct_Z_K_b → 2
elim_K01 → 1
CX01.Z2

@name bifunct_Z_CX_f
Z2.CX10
expand_CX10 → 1
!apply bifunct_Z_K_b → 0
!apply bifunct_Z_CX_e → 1
!apply bifunct_Z_K_b → 2
expand_CX10 ← 0
CX10.Z2


-- *******************************************************************
-- Bifunctoriality Between Pauli-Z and CZ Gates
-- *******************************************************************

@name bifunct_Z_CZ_a
CZ02.Z1
elim_K12 ← 0
!apply drel47 → 1
r25_a ← 2
!apply bifunct_X_CX_e → 1
r25_a_dag → 0
!apply drel48 ← 1
elim_K12 → 2
Z1.CZ02

@name bifunct_Z_CZ_b
CZ12.Z0
expand_CZ12 → 0
!apply elim_SWAP01 ← 4
expand_Z1 ← 2
elim_K12 ← 1
!apply drel47 → 2
r25_a ← 3
!apply bifunct_X_CX_e → 2
r25_a_dag → 1
!apply drel48 ← 2
elim_K12 → 3
expand_Z1 → 1
!apply elim_SWAP01 → 0
expand_CZ12 ← 1
Z0.CZ12


-- *******************************************************************
-- Bifunctoriality Between Pauli-Z Gates and K Gates
-- *******************************************************************

@name bifunct_Z_K_a
Z0.K12
expand_Z0 → 0
r47 → 2
bifunct_X_K_a → 1
r47 → 0
expand_Z0 ← 1
K12.Z0

@name bifunct_Z_K_b
Z2.K01
elim_K12 ← 1
r25_b_dag ← 0
r47 ← 2
bifunct_X_K_b → 1
r47 ← 0
r25_b_dag → 1
elim_K12 → 2
K01.Z2
