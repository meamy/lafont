-- ****************************************************************************
-- The relation W1 = CZ02.CZ01
-- ****************************************************************************

@name wrel1

W1
expand_W1 → 0
!apply srel1 → 0
CZ02.CZ01


-- ****************************************************************************
-- The relation W2 = X1.CZ01.X1
-- ****************************************************************************

@name wrel2

W2
expand_W2 → 0
!apply wrel1 → 2
expand_S5 → 1
expand_S6 → 0

-- CX21.CCX2.CX21.X1.CCX2.X1.CZ02.CZ01.S5.S6
!apply elim_X1 ← 0
!apply elim_CZ01 ← 1
!apply drel119 ← 2

-- X1.CZ01.X1.Z0.CZ01.CX21.CCX2.CX21.X1.CCX2.X1.CZ02.CZ01.S5.S6
!apply drel143 → 5
!apply drel152 → 4
!apply drel114 → 5

-- X1.CZ01.X1.Z0.CCX1.CCX2.CZ02.CCX1.X1.CCX2.X1.CZ02.CZ01.S5.S6
!apply elim_CZ02 ← 8
!apply drel37 → 6
!apply bifunct_X_CZ_a ← 7
!apply bifunct_X_CZ_a → 10
!apply drel151 → 8

-- X1.CZ01.X1.Z0.CCX1.CCX2.CCX1.X1.CCX2.CZ02.CZ01.CZ02.X1.CZ01.S5.S6
!apply drel71 → 10
!apply elim_CZ02 → 9

-- X1.CZ01.X1.Z0.CCX1.CCX2.CCX1.X1.CCX2.CZ01.X1.CZ01.S5.S6
!apply drel119 ← 9
!apply elim_CZ01 → 11

-- X1.CZ01.X1.Z0.CCX1.CCX2.CCX1.X1.CCX2.X1.Z0.S5.S6
!apply drel96 → 7
!apply elim_X1 → 9

-- X1.CZ01.X1.Z0.CCX1.CCX2.CCX1.CCX2.CX02.Z0.S5.S6
r40_a → 4
!apply elim_CCX2 → 6

-- X1.CZ01.X1.Z0.CCX2.CCX1.CX02.Z0.S5.S6
!apply drel40 ← 4
!apply elim_Z0 → 3
!apply drel52 ← 5
!apply elim_Z0 → 4
!apply drel81 → 6
!apply elim_Z0 → 5

-- X1.CZ01.X1.CCX2.CCX1.CX02.S5.S6
expand_S5 → 6

-- X1.CZ01.X1.CCX2.CCX1.CX02.X1.CCX2.X1.S6
!apply drel96 → 6
!apply elim_X1 → 8
!apply drel91 → 6
!apply elim_CX02 → 5

-- X1.CZ01.X1.CCX2.CCX1.CCX2.S6
r40_a ← 3
!apply drel143 ← 3

-- X1.CZ01.X1.CX21.CCX2.CX21.S6
expand_S6 ← 3
elim_s6 → 3
X1.CZ01.X1


-- ****************************************************************************
-- The relation X1.S4.S5 = CX20.CCX0.CCX2.CCX1.X1.CCX0
-- ****************************************************************************

@name wrel3

X1.S4.S5
!apply srel2 → 1

-- X1.CX01.CCX2.CCX0.CX01
elim_CX01 ← 3
r8_c → 4
!apply drel169 → 4

-- X1.CX01.CCX2.CX01.CCX0.CCX1.CCX0
!apply drel7 → 1
!apply elim_X1 → 0
!apply drel131 → 1
!apply drel25 → 3
CCX2.CCX0.CX20.CCX1.X1.CCX0


-- ****************************************************************************
-- The relation CCX0.X1.CCX1.CX20.CCX0.CCX2 = S5.S4.X1
-- ****************************************************************************

@name wrel4

CCX0.X1.CCX1.CX20.CCX0.CCX2
elim_s5 ← 0
elim_s4 ← 1
!apply elim_X1 ← 2
!apply wrel3 → 3
elim_CCX0 → 8
!apply elim_X1 → 7
!apply elim_CCX1 → 6
!apply elim_CX20 → 5
elim_CCX0 → 4
!apply elim_CCX2 → 3
S5.S4.X1


-- ****************************************************************************
-- The relation W3 = CCX0.X1.CCX1.CZ01.CCX1.X1.CCX0
-- ****************************************************************************

@name wrel5

W3
expand_W3 → 0
!apply wrel2 → 2
!apply wrel3 → 4
!apply wrel4 ← 0

-- CCX0.X1.CCX1.CX20.CCX0.CCX2.CZ01.CCX2.CCX0.CX20.CCX1.X1.CCX0
!apply elim_CZ01 ← 8
!apply drel36 → 6
!apply elim_CCX2 → 5

-- CCX0.X1.CCX1.CX20.CCX0.CZ01.CCX0.CX20.CCX1.X1.CCX0
!apply drel159 → 5
elim_CCX0 → 4
r31_c → 4

-- CCX0.X1.CCX1.CX20.CX20.CZ01.CCX1.X1.CCX0
!apply drel138 ← 4
!apply elim_CX20 → 3
CCX0.X1.CCX1.CZ01.CCX1.X1.CCX0


-- ****************************************************************************
-- The relation W4 = Z1.CZ01
-- ****************************************************************************

@name wrel6

W4
expand_W4 → 0
!apply wrel5 → 2

-- S4.S3.CCX0.X1.CCX1.CZ01.CCX1.X1.CCX0.S3.S4
!apply drel25 ← 6
!apply drel25 → 3
!apply drel145 → 4
!apply elim_X1 → 6

-- S4.S3.CCX0.CCX1.CZ01.Z0.CCX1.CCX0.S3.S4
!apply elim_Z0 ← 7
!apply drel52 → 5
!apply drel152 → 4
!apply elim_CCX1 → 3

-- S4.S3.CCX0.CZ01.CZ02.Z0.CCX0.S3.S4
!apply drel23 → 5
!apply drel150 → 4
!apply elim_CZ12 → 6

-- S4.S3.CCX0.CZ01.CCX0.CZ02.Z0.S3.S4
!apply drel159 → 3
elim_CCX0 → 2

-- S4.S3.CZ01.CZ12.CZ02.Z0.S3.S4
!apply srel3 → 6
!apply srel3 → 1

-- S4.CX12.CCX2.CZ01.CZ12.CZ02.Z0.CX12.CCX2.S4
bifunct_Z_CX_b → 6
!apply drel108 → 5
r31_c → 3
!apply drel165 ← 4
!apply drel71 → 5
!apply elim_CZ01 → 6

-- S4.CX12.CCX2.CZ12.CX12.CZ02.Z0.CCX2.S4
!apply elim_CZ12 ← 5
!apply drel158 ← 3
!apply drel80 ← 3

-- S4.CX12.CCX2.CX12.Z1.CZ12.CZ02.Z0.CCX2.S4
!apply drel55 ← 1
!apply elim_CX12 → 2

-- S4.CCX2.Z1.CZ12.CZ02.Z0.CCX2.S4
!apply elim_Z0 ← 7
!apply drel40 → 5
!apply drel151 → 4
!apply drel170 → 3

-- S4.CCX2.Z1.CCX2.CZ12.CZ01.CZ02.CZ01.Z0.S4
!apply drel71 → 5
!apply elim_CZ01 → 6

-- S4.CCX2.Z1.CCX2.CZ12.CZ02.Z0.S4
!apply elim_Z1 ← 4
!apply drel51 → 2
!apply elim_CCX2 → 1

-- S4.Z1.CZ12.CZ02.Z0.S4
expand_S4 → 5
!apply drel78 ← 4
!apply drel166 ← 3
!apply drel109 → 2
!apply elim_CZ02 → 4

-- S4.Z1.CX01.CZ12.Z0.CX02.CCX0.CX02.CX01
!apply drel171 → 1
!apply bifunct_Z_a → 2
!apply bifunct_Z_CZ_b ← 3
!apply elim_Z0 → 4

-- S4.CX01.Z1.CZ12.CX02.CCX0.CX02.CX01
!apply drel172 → 3
!apply bifunct_Z_CX_c → 2

-- S4.CX01.CX02.Z1.CZ12.CZ01.CCX0.CX02.CX01
!apply drel159 → 5
r31_c → 6
!apply drel38 → 4

-- S4.CX01.CX02.Z1.CCX0.CZ01.CX02.CX01
!apply elim_Z1 ← 5
!apply drel53 → 3
!apply drel163 ← 5
!apply bifunct_Z_CX_c → 4

-- S4.CX01.CX02.CCX0.CX02.Z1.CZ01.CX01
!apply elim_CZ01 ← 8
!apply drel104 → 6
!apply drel171 → 5
!apply bifunct_Z_a → 6
!apply elim_Z0 → 7

-- S4.CX01.CX02.CCX0.CX02.CX01.Z1.CZ01
expand_S4 ← 1
elim_s4 → 0
Z1.CZ01


-- ****************************************************************************
-- The relation W5 = Z1.Z2.CZ01.CZ02
-- ****************************************************************************

@name wrel7

W5
expand_W5 → 0
!apply srel4 → 3
!apply wrel6 → 2

-- S3.S2.W4.S2.S3
!apply drel118 ← 3
!apply bifunct_X_Z_a ← 2
!apply elim_Z1 → 3

-- S3.S2.X0.CZ01.CCX2.CCX1.X0
!apply elim_CZ01 ← 5
!apply drel36 → 3

-- S3.S2.X0.CCX2.CZ01.CCX1.X0
!apply drel152 → 4
!apply drel123 ← 6
!apply drel118 ← 5
!apply bifunct_Z_CZ_c → 7

-- S3.S2.X0.CCX2.CCX1.X0.Z1.Z2.CZ01.CZ02
!apply srel4 ← 2
elim_s2 → 1
elim_s3 → 0
Z1.Z2.CZ01.CZ02


-- ****************************************************************************
-- The relation W6 = X0.CZ01.Z0.X0
-- ****************************************************************************

@name wrel8

W6
expand_W6 → 0
!apply srel5 → 3
!apply wrel7 → 2

-- S2.S1.Z1.Z2.CZ01.CZ02.X0.CX02.CCX1.CCX2.X0
!apply drel123 ← 5
!apply bifunct_Z_CZ_c ← 3
!apply bifunct_X_Z_e ← 4
!apply elim_Z2 → 5

-- S2.S1.Z1.CZ01.X0.CZ02.CX02.CCX1.CCX2.X0
!apply drel118 ← 3
!apply bifunct_X_Z_a → 3
!apply elim_Z1 → 2

-- S2.S1.X0.CZ01.CZ02.CX02.CCX1.CCX2.X0
!apply elim_CZ02 ← 6
!apply drel173 ← 4
!apply drel81 ← 4
!apply drel163 ← 3

-- S2.S1.X0.CX02.CZ01.Z0.CZ02.CCX1.CCX2.X0
!apply elim_CZ02 ← 8
!apply drel37 → 6
!apply elim_Z0 ← 7
!apply drel52 → 5

-- S2.S1.X0.CX02.CZ01.CCX1.Z0.CZ02.CCX2.X0
!apply drel152 → 4
!apply drel44 → 6
!apply elim_CZ02 → 7

-- S2.S1.X0.CX02.CCX1.CZ01.Z0.CCX2.X0
!apply elim_Z0 ← 8
!apply drel40 → 6
!apply elim_CZ01 ← 7
!apply drel36 → 5

-- S2.S1.X0.CX02.CCX1.CCX2.CZ01.Z0.X0
elim_X0 ← 6
!apply srel5 ← 2
elim_s1 → 1
elim_s2 → 0
X0.CZ01.Z0.X0


-- ****************************************************************************
-- The relation W6.W4.W2 = CZ01.M
-- ****************************************************************************

@name wrel9

W6.W4.W2
!apply wrel2 → 2
!apply wrel6 → 1

-- W6.Z1.CZ01.X1.CZ01.X1
!apply drel119 ← 2
!apply elim_CZ01 → 4

-- W6.Z1.X1.Z0.X1
!apply bifunct_X_Z_b → 2
!apply elim_X1 → 3

-- W6.Z1.Z0
!apply wrel8 → 0
!apply drel160 → 0
!apply bifunct_Z_a ← 5

-- CZ01.Z1.X0.Z0.X0.Z0.Z1
expand_M ← 2
!apply spatial_Z1 → 2
!apply elim_Z1 → 1
CZ01.M


-- ****************************************************************************
-- The relation CZ02.S6 = S6.CZ01
-- ****************************************************************************

@name wrel10

CZ02.S6
elim_s6 ← 0
!apply elim_CZ01 ← 1
!apply srel10 → 2
!apply elim_CZ02 → 3
elim_s6 → 2
S6.CZ01


-- ****************************************************************************
-- The relation W7 = CCX2
-- ****************************************************************************

W7
expand_W7 → 0
!apply wrel9 → 7
!apply wrel9 → 3

-- S7.S8.S6.CZ01.M.S8.CZ01.M.S6.S8.S7
!apply spatial_CZ01 ← 6
!apply srel9 → 4
!apply elim_M → 5

-- S7.S8.S6.CZ01.S8.CZ01.S6.S8.S7
!apply srel10 → 5
!apply wrel10 ← 2

-- S7.S8.CZ02.S6.S8.S6.CZ02.S8.S7
elim_s8 ← 6
r_s6s8 → 3
S7.S8.CZ02.S8.CZ02.S8.S7
