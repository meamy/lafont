-- ****************************************************************************
-- The relation CZ01.CCX1.CCX2.CCX1 = CCX1.CCX2.CCX1.CZ02
-- ****************************************************************************

@name drel1
CZ01.CCX1.CCX2.CCX1
r26_e → 0
r26_a → 2
r26_e → 4

-- CCX1.CZ01.CCX2.CZ02.CCX1.CZ01.CZ02
!apply drel71 → 5
!apply drel37 → 3

-- CCX1.CZ01.CCX2.CCX1.CZ01
!apply elim_CZ01 ← 3
!apply drel36 → 1

-- CCX1.CCX2.CZ01.CCX1.CZ01
r26_e → 2
!apply drel71 → 3
!apply elim_CZ01 → 4
CCX1.CCX2.CCX1.CZ02


-- ****************************************************************************
-- The relation CX12.CX21.CX12.CCX2 = CCX1.CX12.CX21.CX12
-- ****************************************************************************

@name drel2

CX12.CX21.CX12.CCX2
!apply drel55 ← 2
!apply elim_CX21 ← 3

-- CX12.CX21.CCX2.CX21.CX21.CX12
!apply drel13 → 1
elim_CX12 → 0
CCX1.CX12.CX21.CX12


-- ****************************************************************************
-- The relation CCX2.CCX1.CCX2.CX01 = CX02.CCX2.CCX1.CCX2
-- ****************************************************************************

@name drel3

CCX2.CCX1.CCX2.CX01
!apply elim_CX02 ← 0
r1_a ← 1

-- CX02.CCX2.CX02.CCX1.CCX2.CX01
!apply elim_CX02 ← 4
r10_a → 2
r1_a ← 4

-- CX02.CCX2.CCX1.CX01.CCX2.CX02.CX01
!apply drel9 ← 5
r10_e → 3
!apply elim_CX02 → 4
CX02.CCX2.CCX1.CCX2


-- ****************************************************************************
-- The relation CCX1.CCX2.CCX1.CX02 = CX01.CCX1.CCX2.CCX1
-- ****************************************************************************

@name drel4

CCX1.CCX2.CCX1.CX02
elim_CX01 ← 0
r1_c ← 1

-- CX01.CCX1.CX01.CCX2.CCX1.CX02
elim_CX01 ← 4
r10_e → 2
r1_a → 2
r1_c ← 4
r1_a ← 2

-- CX01.CCX1.CCX2.CX02.CCX1.CX01.CX02
!apply drel9 → 5
r10_a → 3
elim_CX01 → 4
CX01.CCX1.CCX2.CCX1


-- ****************************************************************************
-- The relation CX12.CCX0.CX12 = X2.CCX0.X2
-- ****************************************************************************

@name drel5

CX12.CCX0.CX12
r10_b → 0
!apply elim_X2 ← 0
r5_c → 1

-- X2.CCX0.CX10.X2.CX10
!apply bifunct_X_CX_c → 2
!apply elim_CX10 → 3
X2.CCX0.X2


-- ****************************************************************************
-- The relation CX10.CCX2.CX10 = X0.CCX2.X0
-- ****************************************************************************

@name drel6

CX10.CCX2.CX10
!apply drel90 → 0
elim_X0 ← 0
r5_a → 1

-- X0.CCX2.CX12.X0.CX12
bifunct_X_CX_a → 2
elim_CX12 → 3
X0.CCX2.X0


-- ****************************************************************************
-- The relation CX01.CCX2.CX01 = X1.CCX2.X1
-- ****************************************************************************

@name drel7

CX01.CCX2.CX01
r10_e → 0
!apply elim_X1 ← 0
r5_d → 1

-- X1.CCX2.CX02.X1.CX02
!apply bifunct_X_CX_e → 2
!apply elim_CX02 → 3
X1.CCX2.X1


-- ****************************************************************************
-- The relation CX10.CX20 = CX20.CX10
-- ****************************************************************************

@name drel8

CX10.CX20
r17_b → 0
!apply elim_CX20 → 3
!apply elim_CX20 ← 0
r17_b_dag ← 1
CX20.CX10


-- ****************************************************************************
-- The relation CX01.CX02 = CX02.CX01
-- ****************************************************************************

@name drel9

CX01.CX02
r17_a_dag → 0
!apply elim_CX02 → 3
!apply elim_CX02 ← 0
r17_a ← 1
CX02.CX01


-- ****************************************************************************
-- The relation CX10.CX12 = CX12.CX10
-- ****************************************************************************

@name drel10

CX10.CX12
r17_b_dag → 0
elim_CX12 → 3
elim_CX12 ← 0
r17_b ← 1
CX12.CX10


-- ****************************************************************************
-- The relation CX12.CX02 = CX02.CX12
-- ****************************************************************************

@name drel11

CX12.CX02
r17_c → 1
elim_CX12 → 0
elim_CX12 ← 3
r17_c_dag ← 0
CX02.CX12


-- ****************************************************************************
-- The relation CX20.CX21 = CX21.CX20
-- ****************************************************************************

@name drel12

CX20.CX21
r17_d → 0
!apply elim_CX21 → 3
!apply elim_CX21 ← 0
r17_d_dag ← 1
CX21.CX20


-- ****************************************************************************
-- The relation CX21.CCX2.CX21 = CX12.CCX1.CX12
-- ****************************************************************************

@name drel13

CX21.CCX2.CX21
r8_b → 0
r40_a → 0
r8_f ← 0
CX12.CCX1.CX12


-- ****************************************************************************
-- The relation CZ01.CCX1.CCX0.CCX1 = CCX1.CCX0.CCX1.CZ01
-- ****************************************************************************

@name drel14

CZ01.CCX1.CCX0.CCX1
r26_e → 0
r26_b → 2
r26_d → 4

-- CCX1.CZ01.CCX0.CZ02.CCX1.CZ12.CZ02
r26_c → 1
r31_b ← 6
!apply drel37 → 4

-- CCX1.CCX0.CZ01.CZ12.CCX1.CZ12
r26_d → 3
r26_e → 2
r31_b → 4

-- CCX1.CCX0.CCX1.CZ01.CZ12.CZ02.CZ02.CZ12
!apply elim_CZ02 → 5
!apply elim_CZ12 → 4
CCX1.CCX0.CCX1.CZ01


-- ****************************************************************************
-- The relation CZ12.CCX2.CCX1.CCX2 = CCX2.CCX1.CCX2.CZ12
-- ****************************************************************************

@name drel15

CZ12.CCX2.CCX1.CCX2
r8_f ← 1
!apply elim_CZ12 ← 2
r29_c ← 0

-- Z1.CX12.CZ12.CCX1.CX12
r26_d → 2
!apply drel80 ← 0
!apply drel21 → 1

-- CX12.CCX1.CZ02.Z1.CZ12.CZ02.CX12
!apply bifunct_Z_CZ_a → 2
r31_b → 3
!apply elim_CZ02 → 4

-- CX12.CCX1.Z1.CZ12.CX12
!apply elim_CZ12 ← 5
r29_c ← 3
!apply elim_Z1 → 2
r8_f → 0
CCX2.CCX1.CCX2.CZ12


-- ****************************************************************************
-- The relation CCX0.CCX1.CCX2.CCX1.CCX0 = CCX1.CCX2.CCX1
-- ****************************************************************************

@name drel16

CCX0.CCX1.CCX2.CCX1.CCX0
r40_a → 1
!apply drel17 → 0
r40_a ← 0
CCX1.CCX2.CCX1


-- ****************************************************************************
-- The relation CCX0.CCX2.CCX1.CCX2.CCX0 = CCX2.CCX1.CCX2
-- ****************************************************************************

@name drel17

CCX0.CCX2.CCX1.CCX2.CCX0
r8_f ← 1
elim_CX12 ← 5
elim_CX12 ← 0

-- CX12.CX12.CCX0.CX12.CCX1.CX12.CCX0.CX12.CX12
r10_b → 5
!apply drel56 → 5
r10_b → 1

-- CX12.CCX0.CX10.CCX1.CX10.CCX0.CX12
r8_d → 2
elim_CCX0 → 4
elim_CCX0 → 1
r8_f → 0
CCX2.CCX1.CCX2


-- ****************************************************************************
-- The relation CCX1.CCX0.CCX2.CCX0.CCX1 = CCX0.CCX2.CCX0
-- ****************************************************************************

@name drel18

CCX1.CCX0.CCX2.CCX0.CCX1
r8_e ← 1
!apply elim_CX20 ← 5
!apply elim_CX20 ← 0

-- CX20.CX20.CCX1.CX20.CCX2.CX20.CCX1.CX20.CX20
r10_c → 5
r1_d → 5
r10_c → 1

-- CX20.CCX1.CX21.CCX2.CX21.CCX1.CX20
r8_b → 2
!apply elim_CCX1 → 4
!apply elim_CCX1 → 1
r8_e → 0
CCX0.CCX2.CCX0


-- ****************************************************************************
-- The relation Z2.CX12 = CX12.Z1.Z2
-- ****************************************************************************

@name drel19

Z2.CX12
elim_K12 ← 0
!apply drel64 ← 1
!apply drel30 ← 2

-- K12.X2.CX21.K12
r12_e → 1

-- K12.CX21.X2.X1.K12
!apply drel46 → 0
!apply drel66 → 1
!apply drel65 → 2
elim_K12 → 3
!apply bifunct_Z_c ← 1
CX12.Z1.Z2


-- ****************************************************************************
-- The relation CX12.Z2 = Z2.Z1.CX12
-- ****************************************************************************

@name drel20

CX12.Z2
elim_CX12 ← 2
!apply elim_Z1 ← 3
!apply elim_Z2 ← 4

-- CX12.Z2.CX12.Z1.Z2.Z2.Z1.CX12
!apply drel19 ← 2
!apply elim_Z2 → 1
elim_CX12 → 0
Z2.Z1.CX12


-- ****************************************************************************
-- The relation Z1.CCX1 = CCX1.CZ02.Z1
-- ****************************************************************************

@name drel21

Z1.CCX1
elim_K12 ← 0
r25 ← 1
!apply drel29 ← 2

-- K12.X1.CCX2.K12
r5_d → 1

-- K12.CCX2.CX02.X1.K12
r25 → 3
!apply drel47 ← 2
!apply drel29 → 1
elim_K12 → 0
CCX1.CZ02.Z1


-- ****************************************************************************
-- The relation CCX1.Z1 = Z1.CZ02.CCX1
-- ****************************************************************************

@name drel22

CCX1.Z1
!apply elim_CCX1 ← 2
!apply elim_CZ02 ← 3
!apply elim_Z1 ← 4

-- CCX1.Z1.CCX1.CZ02.Z1.Z1.CZ02.CCX01
!apply drel21 ← 2

-- CCX1.Z1.Z1.CCX1.Z1.CZ02.CCX1
!apply elim_Z1 → 1
!apply elim_CCX1 → 0
Z1.CZ02.CCX1


-- ****************************************************************************
-- The relation Z0.CCX0 = CCX0.CZ12.Z0
-- ****************************************************************************

@name drel23

Z0.CCX0
!apply elim_SWAP01 ← 2
!apply elim_SWAP01 ← 1
!apply elim_SWAP01 ← 0

-- SWAP01.SWAP01.Z0.SWAP01.SWAP01.CCX0.SWAP01.SWAP01
expand_Z1 ← 1
expand_CCX1 ← 2

-- SWAP01.Z1.CCX1.SWAP01
!apply drel21 → 1

-- SWAP01.CCX1.CZ02.Z1.SWAP01
expand_CCX1 → 1
!apply elim_SWAP01 → 0

-- CCX0.SWAP01.CZ02.Z1.SWAP01
expand_Z1 → 3
!apply elim_SWAP01 → 5

-- CCX0.SWAP01.CZ02.SWAP01.Z0
expand_CZ12 ← 1
CCX0.CZ12.Z0


-- ****************************************************************************
-- The relation CCX0.Z0 = Z0.CZ12.CCX0
-- ****************************************************************************

@name drel24

CCX0.Z0
elim_CCX0 ← 2
!apply elim_CZ12 ← 3
!apply elim_Z0 ← 4

-- CCX0.Z0.CCX0.CZ12.Z0.Z0.CZ12.CCX0
!apply drel23 ← 2

-- CCX0.Z0.Z0.CCX0.Z0.CZ12.CCX0
!apply elim_Z0 → 1
elim_CCX0 → 0
Z0.CZ12.CCX0


-- ****************************************************************************
-- The relation X1.CCX1 = CCX1.X1
-- ****************************************************************************

@name drel25

X1.CCX1
expand_CCX1 → 1
expand_X1 → 0

-- SWAP01.X0.SWAP01.SWAP01.CCX0.SWAP01
!apply elim_SWAP01 → 2
r13 → 1
!apply elim_SWAP01 ← 2

-- SWAP01.CCX0.SWAP01.SWAP01.X0.SWAP01
expand_CCX1 ← 0
expand_X1 ← 1
CCX1.X1


-- ****************************************************************************
-- The relation X2.CCX2 = CCX1.X2
-- ****************************************************************************

@name drel26

X2.CCX2
expand_CCX2 → 1
expand_X2 → 0

-- SWAP02.X0.SWAP02.SWAP02.CCX0.SWAP02
!apply elim_SWAP02 → 2
r13 → 1
!apply elim_SWAP02 ← 2

-- SWAP02.CCX0.SWAP02.SWAP02.X0.SWAP02
expand_CCX2 ← 0
expand_X2 ← 1
CCX2.X2


-- ****************************************************************************
-- The relation CZ01.CX21.CX12.CX21 = CX21.CX12.CX21.CZ02
-- ****************************************************************************

@name drel27

CZ01.CX21.CX12.CX21
r7 ← 1
expand_SWAP12 ← 1

-- CZ01.SWAP12
!apply elim_SWAP12 ← 0
expand_CZ02 ← 1

-- SWAP12.CZ02
expand_SWAP12 → 0
r7 → 0
CX21.CX12.CX21.CZ02


-- ****************************************************************************
-- The relation CX21.CX12.CX21.CZ01 = CZ02.CX21.CX12.CX21
-- ****************************************************************************

@name drel28

CX21.CX12.CX21.CZ01
!apply elim_SWAP12 ← 4
!apply elim_CZ02 ← 5

-- CX21.CX12.CX21.CZ01.SWAP12.CZ02.CZ02.SWAP12
expand_SWAP12 → 4
r7 → 4
!apply drel27 ← 4

-- CX21.CX12.CX21.CZ01.CZ01.CX21.CX12.CX21.CZ02.SWAP12
!apply elim_CZ01 → 3
!apply elim_CX21 → 2
elim_CX12 → 1
!apply elim_CX21 → 0

-- CZ02.SWAP12
expand_SWAP12 → 1
r7 → 1
CZ02.CX21.CX12.CX21


-- ****************************************************************************
-- The relation CCX2.K12 = K12.CCX1
-- ****************************************************************************

@name drel29

CCX2.K12
!apply elim_CCX1 ← 2
elim_K12 ← 3

-- CCX2.K12.CCX1.K12.K12.CCX1
r32 ← 2

-- CCX2.K12.K12.CCX2.K12.CCX1
elim_K12 → 1
!apply elim_CCX2 → 0
K12.CCX1


-- ****************************************************************************
-- The relation CX21.K12 = K12.CX12
-- ****************************************************************************

@name drel30

CX21.K12
elim_CX12 ← 2
elim_K12 ← 3

-- CX21.K12.CX12.K12.K12.CX12
!apply drel46 ← 2

-- CX21.K12.K12.CX21.K12.CX12
elim_K12 → 1
!apply elim_CX21 → 0
K12.CX12


-- ****************************************************************************
-- The relation X1.Z1 = M.Z1.X1
-- ****************************************************************************

@name drel31

X1.Z1
!apply elim_X1 ← 2
!apply elim_Z1 ← 3
!apply expand_M_alt1 ← 0
M.Z1.X1


-- ****************************************************************************
-- The relation X0.Z0 = M.Z0.X0
-- ****************************************************************************

@name drel32

X0.Z0
elim_X0 ← 2
!apply elim_Z0 ← 3
expand_M ← 0
M.Z0.X0


-- ****************************************************************************
-- The relation X2.Z2 = M.Z2.X2
-- ****************************************************************************

@name drel33

X2.Z2
!apply elim_X2 ← 2
!apply elim_Z2 ← 3
!apply expand_M_alt2 ← 0
M.Z2.X2


-- ****************************************************************************
-- The relation Z1.X1 = X1.Z1.M
-- ****************************************************************************

@name drel34

Z1.X1
!apply elim_M ← 2
!apply elim_Z1 ← 3
!apply elim_X1 ← 4

-- Z1.X1.M.Z1.X1.X1.Z1.M
!apply drel31 ← 2

-- Z1.X1.X1.Z1.X1.Z1.M
!apply elim_X1 → 1
!apply elim_Z1 → 0
X1.Z1.M


-- ****************************************************************************
-- The relation Z2.X2 = X2.Z2.M
-- ****************************************************************************

@name drel35

Z2.X2
!apply elim_M ← 2
!apply elim_Z2 ← 3
!apply elim_X2 ← 4

-- Z2.X2.M.Z2.X2.X2.Z2.M
!apply drel33 ← 2

-- Z2.X2.X2.Z2.X2.Z2.M
!apply elim_X2 → 1
!apply elim_Z2 → 0
X2.Z2.M


-- ****************************************************************************
-- The relation CZ01.CCX2.CZ01 = CCX2
-- ****************************************************************************

@name drel36

CZ01.CCX2.CZ01
elim_K12 ← 0
!apply drel49 → 1
r32 → 2
!apply drel49 → 3

-- K12.CX01.CCX1.CX01.K12
r1_c → 2
elim_CX01 → 1

-- K12.CCX1.K12
r32 ← 1
elim_K12 → 0
CCX2


-- ****************************************************************************
-- The relation CZ02.CCX1.CZ02 = CCX1
-- ****************************************************************************

@name drel37

CZ02.CCX1.CZ02
elim_K12 ← 2
!apply drel47 → 3
r32 ← 1
!apply drel48 → 0

-- K12.CX02.CCX2.CX02.K12
r1_a → 2
!apply elim_CX02 → 1

-- K12.CCX2.K12
r32 → 0
elim_K12 → 1
CCX1


-- ****************************************************************************
-- The relation CZ12.CCX0.CZ12 = CCX0
-- ****************************************************************************

@name drel38

CZ12.CCX0.CZ12
expand_CZ12 → 2
expand_CZ12 → 0
expand_CCX1 ← 2

-- SWAP01.CZ02.CCX1.CZ02.SWAP01
!apply drel37 → 1

-- SWAP01.CCX1.SWAP01
expand_CCX1 → 1
!apply elim_SWAP01 → 3
!apply elim_SWAP01 → 0
CCX0


-- ****************************************************************************
-- The relation Z2.CCX1.Z2 = CCX1
-- ****************************************************************************

@name drel39

Z2.CCX1.Z2
elim_K12 ← 0
!apply drel64 ← 1

-- K12.X2.K12.CCX1.Z2
elim_K12 ← 4
r32 ← 3
elim_K12 → 2

-- K12.X2.CCX2.K12.Z2
!apply drel64 ← 3

-- K12.X2.CCX2.X2.K12
!apply drel26 → 1
!apply elim_X2 → 2

-- K12.CCX2.K12
r32 → 0
elim_K12 → 1
CCX1


-- ****************************************************************************
-- The relation Z0.CCX2.Z0 = CCX2
-- ****************************************************************************

@name drel40

Z0.CCX2.Z0
!apply elim_SWAP01 ← 3
!apply elim_SWAP01 ← 0
!apply symmetry_CCX_c → 3

-- SWAP01.SWAP01.Z0.SWAP01.CCX2.SWAP01.Z0.SWAP01.SWAP01
expand_Z1 ← 5
expand_Z1 ← 1

-- SWAP01.Z1.CCX2.Z1.SWAP01
!apply drel51 → 1
!apply symmetry_CCX_c ← 0
CCX2


-- ****************************************************************************
-- The relation CX01.CX21 = CX21.CX01
-- ****************************************************************************

@name drel41
CX01.CX21
r17_a → 0
!apply elim_CX21 → 3
!apply elim_CX21 ← 0
r17_a_dag ← 1
CX21.CX01


-- ****************************************************************************
-- The relation Z0.CX10 = CX10.Z1.Z0
-- ****************************************************************************

@name drel42

Z0.CX10
!apply elim_SWAP02 ← 2
!apply elim_SWAP02 ← 1
!apply elim_SWAP02 ← 0

-- SWAP02.SWAP02.Z0.SWAP02.SWAP02.CX10.SWAP02.SWAP02
expand_Z2 ← 1
!apply symmetry_CX_f ← 2
!apply drel19 → 1

-- SWAP02.CX12.Z1.Z2.SWAP02
!apply elim_SWAP02 ← 3
!apply elim_SWAP02 ← 2

-- SWAP02.CX12.SWAP02.SWAP02.Z1.SWAP02.SWAP02.Z2.SWAP02
!apply symmetry_Z_a ← 3
!apply symmetry_Z_b ← 4
symmetry_CX_b ← 0
CX10.Z1.Z0


-- ****************************************************************************
-- The relation CZ01.Z0 = Z0.CZ01
-- ****************************************************************************

@name drel43

CZ01.Z0
elim_K12 ← 0
!apply drel49 → 1
!apply bifunct_Z_K_a ← 2

-- K12.CZ01.Z0.K12
!apply drel78 → 1

-- K12.Z0.CZ01.K12
!apply bifunct_Z_K_a ← 0
!apply drel50 ← 1
elim_K12 → 2
Z0.CZ01


-- ****************************************************************************
-- The relation CZ02.Z0 = Z0.CZ02
-- ****************************************************************************

@name drel44

CZ02.Z0
elim_K12 ← 0
!apply drel47 → 1
!apply bifunct_Z_K_a ← 2

-- K12.CX02.Z0.K12
expand_CX02 → 1
!apply bifunct_Z_SWAP_a ← 3
!apply drel78 → 2
!apply bifunct_Z_SWAP_a ← 1
!apply bifunct_Z_K_a ← 0

-- Z0.K12.SWAP12.CX01.SWAP12.K12
expand_CX02 ← 2

-- Z0.K12.CX02.K12
!apply drel47 ← 2
elim_K12 → 1
Z0.CZ02


-- ****************************************************************************
-- The relation CZ01.Z1 = CZ01.Z1
-- ****************************************************************************

@name drel45

CZ01.Z1
elim_K12 ← 0
!apply drel49 → 1

-- K12.CX01.K12.Z1
r25 ← 2

-- K12.CX01.X1.K12
!apply elim_X1 ← 1
!apply drel82 → 2

-- K12.X1.CX01.K12
!apply drel65 → 0
!apply drel50 ← 1
elim_K12 → 2
Z1.CZ01


-- ****************************************************************************
-- The relation K12.CX21 = CX12.K12
-- ****************************************************************************

@name drel46

K12.CX21
expand_CX21 → 1
elim_K12 → 0
CX12.K12


-- ****************************************************************************
-- The relation K12.CZ02 = CX02.K12
-- ****************************************************************************

@name drel47

K12.CZ02
expand_CZ02 → 1
!apply elim_SWAP12 ← 0
!apply symmetry_K_a ← 1

-- SWAP12.K12.CZ01.SWAP12
expand_CZ01 → 2
elim_K12 → 1

-- SWAP12.CX01.K12.SWAP12
!apply elim_SWAP12 ← 2
!apply symmetry_K_a ← 3

-- SWAP12.CX01.SWAP12.K12
expand_CX02 ← 0
CX02.K12


-- ****************************************************************************
-- The relation CZ02.K12 = K12.CX02
-- ****************************************************************************

@name drel48

CZ02.K12
!apply elim_CX02 ← 2
elim_K12 ← 3

-- CZ02.K12.CX02.K12.K12.CX02
!apply drel47 ← 2

-- CZ02.K12.K12.CZ02.K12.CX02
elim_K12 → 1
!apply elim_CZ02 → 0
K12.CX02


-- ****************************************************************************
-- The relation K12.CZ01 = CX01.K12
-- ****************************************************************************

@name drel49

K12.CZ01
expand_CZ01 → 1
elim_K12 → 0
CX01.K12


-- ****************************************************************************
-- The relation CZ01.K12 = K12.CX01
-- ****************************************************************************

@name drel50

CZ01.K12
elim_CX01 ← 2
elim_K12 ← 3

-- CZ01.K12.CX01.K12.K12.CX01
!apply drel49 ← 2

-- CZ01.K12.K12.CZ01.K12.CX01
elim_K12 → 1
!apply elim_CZ01 → 0
K12.CX01


-- ****************************************************************************
-- The relation Z1.CCX2.Z1 = CCX2
-- ****************************************************************************

@name drel51

Z1.CCX2.Z1
elim_K12 ← 0

-- K12.K12.Z1.CCX2.Z1
r25 ← 1
r32 → 2
r25 ← 3

-- K12.X1.CCX1.X1.K12
!apply drel25 → 1
!apply elim_X1 → 2

-- K12.CCX1.K12
r32 ← 1
elim_K12 → 0
CCX2


-- ****************************************************************************
-- The relation Z0.CCX1.Z0 = CCX1
-- ****************************************************************************

@name drel52

Z0.CCX1.Z0
expand_Z0 → 2
expand_Z0 → 0

-- K01.X0.K01.CCX1.K01.X0.K01
!apply drel63 ← 3
elim_K01 → 2

--K01.X0.CCX0.X0.K01
r13 → 1
elim_X0 → 2

-- K01.CCX0.K01
!apply drel63 → 0
elim_K01 → 1
CCX1


-- ****************************************************************************
-- The relation Z1.CCX0.Z1 = CCX0
-- ****************************************************************************

@name drel53

Z1.CCX0.Z1
expand_Z1 → 2
expand_Z1 → 0

-- SWAP01.Z0.SWAP01.CCX0.SWAP01.Z0.SWAP01
expand_CCX1 ← 2
!apply drel52 → 1

-- SWAP01.CCX1.SWAP01
expand_CCX1 → 1
!apply elim_SWAP01 → 3
!apply elim_SWAP01 → 0
CCX0


-- ****************************************************************************
-- The relation Z2.CCX0.Z2 = CCX0
-- ****************************************************************************

@name drel54

Z2.CCX0.Z2
elim_K01 ← 0
!apply bifunct_Z_K_b ← 1
!apply drel63 → 2
!apply bifunct_Z_K_b ← 3

-- K01.Z2.CCX1.Z2.K01
elim_K12 ← 1
!apply drel64 ← 2
!apply drel29 ← 3
!apply drel64 ← 4

-- K01.K12.X2.CCX2.X2.K12.K01
!apply drel26 → 2
!apply elim_X2 → 3

-- K01.K12.CCX2.K12.K01
!apply drel29 → 2
elim_K12 → 1

-- K01.CCX1.K01
!apply drel63 ← 1
elim_K01 → 0
CCX0


-- ****************************************************************************
-- The relation CCX2.CX12 = CX12.CCX2
-- ****************************************************************************

@name drel55

CCX2.CX12
elim_K12 ← 0

-- K12.K12.CCX2.CX12
r32 → 1
elim_K12 ← 4
expand_CX21 ← 2

-- K12.CCX1.CX21.K12
r1_d → 1
r32 ← 2
expand_CX21 → 1

-- K12.K12.CX12.K12.K12.CCX2
elim_K12 → 3
elim_K12 → 0
CX12.CCX2


-- ****************************************************************************
-- The relation CCX0.CX10 = CX10.CCX0
-- ****************************************************************************

@name drel56

CCX0.CX10
elim_K01 ← 0

-- K01.K01.CCX0.CX10
!apply drel63 → 1
expand_CX10 → 3
elim_K01 → 2

-- K01.CCX1.CX01.K01
r1_c → 1

-- K01.CX01.CCX1.K01
!apply drel63 ← 2
expand_CX10 ← 0
CX10.CCX0


-- ****************************************************************************
-- The relation CCX0.CZ02 = CZ12.CZ02.CCX0
-- ****************************************************************************

@name drel57

CCX0.CZ02
elim_CCX0 ← 2
!apply elim_CZ02 ← 3
!apply elim_CZ12 ← 4

-- CCX0.CZ02.CCX0.CZ02.CZ12.CZ12.CZ02.CCX0
r26_b ← 2

-- CCX0.CZ02.CZ02.CCX0.CZ12.CZ02.CCX0
!apply elim_CZ02 → 1
elim_CCX0 → 0

CZ12.CZ02.CCX0


-- ****************************************************************************
-- The relation Z1.K01 = K01.X1
-- ****************************************************************************

@name drel58

Z1.K01
expand_Z1 → 0
!apply symmetry_K_b → 3
!apply elim_SWAP01 → 2

-- SWAP01.Z0.K01.SWAP01
expand_Z0 → 1
elim_K01 → 3

-- SWAP01.K01.X0.SWAP01
!apply symmetry_K_b → 1
!apply elim_SWAP01 → 0
expand_X1 ← 1
K01.X1


-- ****************************************************************************
-- The relation X1.K01 = K01.Z1
-- ****************************************************************************

@name drel59

X1.K01
!apply elim_Z1 ← 2
elim_K01 ← 3
!apply drel58 → 2
elim_K01 → 1
!apply elim_X1 → 0
K01.Z1


-- ****************************************************************************
-- The relation CZ12.Z2 = Z2.CZ12
-- ****************************************************************************

@name drel60

CZ12.Z2
expand_CZ12 → 0
!apply bifunct_Z_SWAP_c ← 2
!apply drel62 → 1
!apply bifunct_Z_SWAP_c ← 0
expand_CZ12 ← 1
Z2.CZ12


-- ****************************************************************************
-- The relation CX02.X2 = CX02.X2
-- ****************************************************************************

@name drel61

CX02.X2
!apply symmetry_X_c → 1
expand_CX02 → 0
!apply elim_SWAP12 → 2

-- SWAP12.CX01.X1.SWAP12
!apply elim_X1 ← 1
!apply drel82 → 2

-- SWAP12.X1.CX01.SWAP12
!apply elim_SWAP12 ← 2
expand_CX02 ← 3
!apply symmetry_X_c ← 0
X2.CX02


-- ****************************************************************************
-- The relation CZ02.Z2 = Z2.CZ02
-- ****************************************************************************

@name drel62

CZ02.Z2
elim_K12 ← 0
!apply drel47 → 1
!apply drel64 ← 2

-- K12.CX02.X2.K12
!apply drel61 → 1

-- K12.X2.CX02.K12
!apply drel47 ← 2
!apply drel64 → 1
elim_K12 → 0
Z2.CZ02


-- ****************************************************************************
-- The relation K01.CCX0 = CCX1.K01
-- ****************************************************************************

@name drel63

K01.CCX0
symmetry_K_c → 0
!apply elim_SWAP02 ← 4
expand_CCX2 ← 2

-- SWAP02.K12.CCX2.SWAP02
r32 → 1

-- SWAP02.CCX1.K12.SWAP02
!apply elim_SWAP02 ← 2
symmetry_K_c ← 3
!apply symmetry_CCX_f ← 0
CCX1.K01


-- ****************************************************************************
-- The relation X2.K12 = K12.Z2
-- ****************************************************************************

@name drel64

X2.K12
expand_X2 → 0
!apply elim_SWAP02 ← 4
symmetry_K_c ← 2

-- SWAP02.X0.K01.SWAP02
elim_K01 ← 1
expand_Z0 ← 2

-- SWAP02.K01.Z0.SWAP02
symmetry_K_c → 1
!apply elim_SWAP02 → 0

-- K12.SWAP02.Z0.SWAP02
expand_Z2 ← 1
K12.Z2


-- ****************************************************************************
-- The relation K12.X1 = Z1.K12
-- ****************************************************************************

@name drel65

K12.X1
elim_K12 ← 2
!apply elim_Z1 ← 3

-- K12.X1.K12.Z1.Z1.K12
r25 ← 2

-- K12.X1.X1.K12.Z1.K12
!apply elim_X1 → 1
elim_K12 → 0
Z1.K12


-- ****************************************************************************
-- The relation K12.X2 = Z2.K12
-- ****************************************************************************

@name drel66

K12.X2
elim_K12 ← 2
!apply elim_Z2 ← 3

-- K12.X2.K12.Z2.Z2.K12
!apply drel64 ← 2

-- K12.X2.X2.K12.Z2.K12
!apply elim_X2 → 1
elim_K12 → 0
Z2.K12


-- ****************************************************************************
-- The relation X1.CX10 = CX10.X1.X0
-- ****************************************************************************

@name drel67

X1.CX10
expand_X1 → 0
!apply elim_SWAP01 ← 4
!apply symmetry_CX_e ← 2

-- SWAP01.X0.CX01.SWAP01
r12_a → 1

-- SWAP01.CX01.X0.X1.SWAP01
!apply symmetry_CX_e → 1
!apply elim_SWAP01 → 0
expand_X1 → 3
!apply elim_SWAP01 → 5
expand_X1 ← 1
CX10.X1.X0


-- ****************************************************************************
-- The relation CX10.X1 = X0.X1.CX10
-- ****************************************************************************

@name drel68

CX10.X1
!apply elim_CX10 ← 2
!apply  elim_X1 ← 3
elim_X0 ← 4

-- CX10.X1.CX10.X1.X0.X0.X1.CX10
!apply drel67 ← 2

-- CX10.X1.X1.CX10.X0.X1.CX10
!apply elim_X1 → 1
!apply elim_CX10 → 0
X0.X1.CX10


-- ****************************************************************************
-- The relation X1.CX12 = CX12.X1.X2
-- ****************************************************************************

@name drel69

X1.CX12
!apply symmetry_CX_f → 1
!apply bifunct_X_SWAP_c → 0

-- SWAP02.X1.CX10.SWAP02
!apply drel67 → 1

-- SWAP02.CX10.X1.X0.SWAP02
!apply elim_SWAP02 ← 3
expand_X2 ← 4

-- SWAP02.CX10.X1.SWAP02.X2
!apply bifunct_X_SWAP_c → 2
!apply symmetry_CX_f ← 0
CX12.X1.X2


-- ****************************************************************************
-- The relation CX12.X1 = X2.X1.CX12
-- ****************************************************************************

@name drel70

CX12.X1
elim_CX12 ← 2
!apply elim_X1 ← 3
!apply elim_X2 ← 4

-- CX12.X1.CX12.X1.X2.X2.X1.CX12
!apply drel69 ← 2

-- CX12.X1.X1.CX12.X2.X1.CX12
!apply elim_X1 → 1
elim_CX12 → 0
X2.X1.CX12


-- ****************************************************************************
-- The relation CZ01.CZ02 = CZ02.CZ01
-- ****************************************************************************

@name drel71

CZ01.CZ02
elim_K12 ← 0
!apply drel49 → 1
!apply drel47 → 2

-- K12.CX01.CX02.K12
!apply drel9 → 1

-- K12.CX02.CX01.K12
!apply drel48 ← 0
!apply drel50 ← 1
elim_K12 → 2
CZ02.CZ01


-- ****************************************************************************
-- The relation Z0.CX20 = CX20.Z2.Z0
-- ****************************************************************************

@name drel72

Z0.CX20
!apply symmetry_CX_j → 1
!apply bifunct_Z_SWAP_a → 0

-- SWAP12.Z0.CX10.SWAP12
!apply drel42 → 1

-- SWAP12.CX10.Z2.Z0.SWAP12
!apply bifunct_Z_SWAP_a → 3
!apply elim_SWAP12 ← 2
!apply symmetry_Z_c ← 3

-- SWAP12.CX10.SWAP12.Z2.Z0
!apply symmetry_CX_j ← 0
CX20.Z2.Z0


-- ****************************************************************************
-- The relation CX20.Z0 = Z0.Z2.CX20
-- ****************************************************************************

@name drel73

CX20.Z0
!apply elim_CX20 ← 2
!apply elim_Z2 ← 3
!apply elim_Z0 ← 4

-- CX20.Z0.CX20.Z2.Z0.Z0.Z2.CX20
!apply drel72 ← 2

-- CX20.Z0.Z0.CX20.Z0.Z2.CX20
!apply elim_Z0 → 1
!apply elim_CX20 → 0
Z0.Z2.CX20


-- ****************************************************************************
-- The relation K01.CX20 = CZ02.K01
-- ****************************************************************************

@name drel74

K01.CX20
expand_CX20 → 1
symmetry_K_c → 0
!apply elim_SWAP02 → 2

-- SWAP02.K12.CX02.SWAP02
!apply drel48 ← 1

-- SWAP02.CZ02.K12.SWAP02
!apply elim_SWAP02 ← 2
symmetry_K_c ← 3
!apply symmetry_CZ_a ← 0
CZ02.K01


-- ****************************************************************************
-- The relation CX10.Z0 = Z0.Z1.CX10
-- ****************************************************************************

@name drel75

CX10.Z0
!apply elim_CX10 ← 2
!apply elim_Z1 ← 3
!apply elim_Z0 ← 4

-- CX10.Z0.CX10.Z1.Z0.Z0.Z1.CX10
!apply drel42 ← 2

-- CX10.Z0.Z0.CX10.Z0.Z1.CX10
!apply elim_Z0 → 1
!apply elim_CX10 → 0
Z0.Z1.CX10


-- ****************************************************************************
-- The relation Z2.CX21 = CZ12.CX21.CZ12
-- ****************************************************************************

@name drel76

Z2.CX21
!apply symmetry_CX_i → 1
expand_Z2 → 0
!apply elim_SWAP02 → 2

-- SWAP02.Z0.CX01.SWAP02
r29_d → 1

-- SWAP02.CZ01.CX01.CZ01.SWAP02
!apply elim_SWAP02 ← 3
!apply symmetry_CZ_d ← 4

-- SWAP02.CZ01.CX01.SWAP02.CZ12
!apply elim_SWAP02 ← 2
!apply symmetry_CX_i ← 3
!apply symmetry_CZ_d ← 0
CZ12.CX21.CZ12


-- ****************************************************************************
-- The relation CX10.Z1 = Z1.CX10
-- ****************************************************************************

@name drel77

CX10.Z1
expand_CX10 → 0
!apply drel59 ← 2

-- K01.CX01.X1.K01
!apply elim_X1 ← 1
!apply drel82 → 2

-- K01.X1.CX01.K01
!apply drel58 ← 0
expand_CX10 ← 1
Z1.CX10


-- ****************************************************************************
-- The relation CX01.Z0 = Z0.CX01
-- ****************************************************************************

@name drel78

CX01.Z0
!apply elim_SWAP01 ← 2
!apply symmetry_CX_e → 0
expand_Z1 ← 2

-- SWAP01.CX10.Z1.SWAP01
!apply drel77 → 1

-- SWAP01.Z1.CX10.SWAP01
expand_Z1 → 1
!apply elim_SWAP01 → 0
!apply symmetry_CX_e ← 1
Z0.CX01


-- ****************************************************************************
-- The relation X1.CX21.X1 = CX21
-- ****************************************************************************

@name drel79

X1.CX21.X1
expand_X1 → 2
expand_X1 → 0

-- SWAP01.X0.SWAP01.CX21.SWAP01.X0.SWAP01
!apply symmetry_CX_h ← 2

-- SWAP01.X0.CX20.X0.SWAP01
!apply drel83 → 1

-- SWAP01.CX20.SWAP01
!apply symmetry_CX_h → 1
!apply elim_SWAP01 → 3
!apply elim_SWAP01 → 0
CX21


-- ****************************************************************************
-- The relation CX12.Z1 = Z1.CX12
-- ****************************************************************************

@name drel80

CX12.Z1
elim_K12 ← 0
!apply drel30 ← 1
r25 ← 2

-- K12.CX21.X1.K12
!apply elim_X1 ← 1
!apply drel79 → 2

-- K12.X1.CX21.K12
!apply drel65 → 0
!apply drel46 → 1
elim_K12 → 2
Z1.CX12


-- ****************************************************************************
-- The relation CX02.Z0 = Z0.CX02
-- ****************************************************************************

@name drel81

CX02.Z0
expand_CX02 → 0
!apply bifunct_Z_SWAP_a ← 2

-- SWAP12.CX01.Z0.SWAP12
!apply drel78 → 1

-- SWAP12.Z0.CX01.SWAP12
!apply bifunct_Z_SWAP_a ← 0
expand_CX02 ← 1
Z0.CX02


-- ****************************************************************************
-- The relation X1.CX01.X1 = CX01
-- ****************************************************************************

@name drel82

X1.CX01.X1
expand_X1 → 2
expand_X1 → 0

-- SWAP01.X1.SWAP01.CX01.SWAP01.X1.SWAP01
!apply symmetry_CX_e → 3
!apply elim_SWAP01 → 5
!apply elim_SWAP01 → 2

-- SWAP01.X0.CX10.X0.SWAP01
r36 → 1
!apply symmetry_CX_e ← 0
CX01


-- ****************************************************************************
-- The relation X0.CX20.X0 = CX20
-- ****************************************************************************

@name drel83

X0.CX20.X0
!apply symmetry_CX_j → 1

-- X0.SWAP12.CX10.SWAP12.X0
!apply bifunct_X_SWAP_a → 0
!apply bifunct_X_SWAP_a ← 3

-- SWAP12.X0.CX10.X0.SWAP12
r36 → 1

-- SWAP12.CX10.SWAP12
!apply symmetry_CX_j ← 0
CX20


-- ****************************************************************************
-- The relation X2.CX12.X2 = CX12
-- ****************************************************************************

@name drel84

X2.CX12.X2
expand_X2 → 2
expand_X2 → 0

-- SWAP02.X0.SWAP02.CX12.SWAP02.X0.SWAP02
!apply symmetry_CX_f → 3
!apply elim_SWAP02 → 5
!apply elim_SWAP02 → 2

-- SWAP02.X0.CX10.X0.SWAP02
r36 → 1

-- SWAP02.CX10.SWAP02
!apply symmetry_CX_f ← 0
CX12


-- ****************************************************************************
-- The relation CCX0.K01 = K01.CCX1
-- ****************************************************************************

@name drel85

CCX0.K01
elim_K01 ← 0
!apply drel63 → 1
elim_K01 → 2
K01.CCX1


-- ****************************************************************************
-- The relation K12.CCX2 = CCX1.K12
-- ****************************************************************************

@name drel86

K12.CCX2
elim_K12 ← 2
!apply drel29 → 1
elim_K12 → 0
CCX1.K12


-- ****************************************************************************
-- The relation CZ12.K01 = K01.CX21
-- ****************************************************************************

@name drel89

CZ12.K01
expand_CZ12 → 0
!apply symmetry_K_b → 3
!apply elim_SWAP01 → 2

-- SWAP01.CZ02.K01.SWAP01
!apply drel74 ← 1

-- SWAP01.K01.CX20.SWAP01
!apply symmetry_K_b → 1
!apply elim_SWAP01 → 0

-- K01.SWAP01.CX20.SWAP01
!apply symmetry_CX_h → 2
!apply elim_SWAP01 → 4
!apply elim_SWAP01 → 1
K01.CX21


-- ****************************************************************************
-- The relation CX10.CCX2.CX10 = CCX2.CX12
-- ****************************************************************************

@name drel90

CX10.CCX2.CX10
!apply elim_SWAP02 ← 3
!apply elim_SWAP02 ← 0

-- SWAP02.SWAP02.CX10.CCX2.CX10.SWAP02.SWAP02
expand_CCX2 → 3
!apply symmetry_CX_f ← 5
!apply symmetry_CX_f ← 1

-- SWAP02.CX12.CCX0.CX12.SWAP02
r10_b → 1

-- SWAP02.CCX0.CX10.SWAP02
!apply elim_SWAP02 ← 2
!apply symmetry_CX_f ← 3
expand_CCX2 ← 0
CCX2.CX12
