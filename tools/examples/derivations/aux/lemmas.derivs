-- ****************************************************************************
-- The relation CZ01.CCX1.CCX2.CCX1 = CCX1.CCX2.CCX1.CZ02
-- ****************************************************************************

@name drel1
CZ01.CCX1.CCX2.CCX1
r26_e → 0
r26_a → 2
r26_e → 4

-- CCX1.CZ01.CCX2.CZ02.CCX1.CZ01.CZ02
r31_a → 5
!apply drel37 → 3

-- CCX1.CZ01.CCX2.CCX1.CZ01
elim_CZ01 ← 3
!apply drel36 → 1

-- CCX1.CCX2.CZ01.CCX1.CZ01
r26_e → 2
r31_a → 3
elim_CZ01 → 4
CCX1.CCX2.CCX1.CZ02


-- ****************************************************************************
-- The relation CX12.CX21.CX12.CCX2 = CCX1.CX12.CX21.CX12
-- ****************************************************************************

@name drel2

CX12.CX21.CX12.CCX2
r1_f ← 2
elim_CX21 ← 3

-- CX12.CX21.CCX2.CX21.CX21.CX12
!apply drel13 → 1
elim_CX12 → 0
CCX1.CX12.CX21.CX12


-- ****************************************************************************
-- The relation CCX2.CCX1.CCX2.CX01 = CX02.CCX2.CCX1.CCX2
-- ****************************************************************************

@name drel3

CCX2.CCX1.CCX2.CX01
!apply elim_CX02 ← 0
r1_a ← 1

-- CX02.CCX2.CX02.CCX1.CCX2.CX01
!apply elim_CX02 ← 4
r10_a → 2
r1_a ← 4

-- CX02.CCX2.CCX1.CX01.CCX2.CX02.CX01
!apply drel9 ← 5
r10_e → 3
!apply elim_CX02 → 4
CX02.CCX2.CCX1.CCX2


-- ****************************************************************************
-- The relation CCX1.CCX2.CCX1.CX02 = CX01.CCX1.CCX2.CCX1
-- ****************************************************************************

@name drel4

CCX1.CCX2.CCX1.CX02
elim_CX01 ← 0
r1_c ← 1

-- CX01.CCX1.CX01.CCX2.CCX1.CX02
elim_CX01 ← 4
r10_e → 2
r1_a → 2
r1_c ← 4
r1_a ← 2

-- CX01.CCX1.CCX2.CX02.CCX1.CX01.CX02
!apply drel9 → 5
r10_a → 3
elim_CX01 → 4
CX01.CCX1.CCX2.CCX1


-- ****************************************************************************
-- The relation CX12.CCX0.CX12 = X2.CCX0.X2
-- ****************************************************************************

@name drel5

CX12.CCX0.CX12
r10_b → 0
!apply elim_X2 ← 0
r5_c → 1

-- X2.CCX0.CX10.X2.CX10
bifunct_X_CX_c → 2
elim_CX10 → 3
X2.CCX0.X2


-- ****************************************************************************
-- The relation CX10.CCX2.CX10 = X0.CCX2.X0
-- ****************************************************************************

@name drel6

CX10.CCX2.CX10
r10_d → 0
elim_X0 ← 0
r5_a → 1

-- X0.CCX2.CX12.X0.CX12
bifunct_X_CX_a → 2
elim_CX12 → 3
X0.CCX2.X0


-- ****************************************************************************
-- The relation CX01.CCX2.CX01 = X1.CCX2.X1
-- ****************************************************************************

@name drel7

CX01.CCX2.CX01
r10_e → 0
!apply elim_X1 ← 0
r5_d → 1

-- X1.CCX2.CX02.X1.CX02
bifunct_X_CX_e → 2
!apply elim_CX02 → 3
X1.CCX2.X1


-- ****************************************************************************
-- The relation CX10.CX20 = CX20.CX10
-- ****************************************************************************

@name drel8

CX10.CX20
r17_b → 0
!apply elim_CX20 → 3
!apply elim_CX20 ← 0
r17_b_dag ← 1
CX20.CX10


-- ****************************************************************************
-- The relation CX01.CX02 = CX02.CX01
-- ****************************************************************************

@name drel9

CX01.CX02
r17_a_dag → 0
!apply elim_CX02 → 3
!apply elim_CX02 ← 0
r17_a ← 1
CX02.CX01


-- ****************************************************************************
-- The relation CX10.CX12 = CX12.CX10
-- ****************************************************************************

@name drel10

CX10.CX12
r17_b_dag → 0
elim_CX12 → 3
elim_CX12 ← 0
r17_b ← 1
CX12.CX10


-- ****************************************************************************
-- The relation CX12.CX02 = CX02.CX12
-- ****************************************************************************

@name drel11

CX12.CX02
r17_c → 1
elim_CX12 → 0
elim_CX12 ← 3
r17_c_dag ← 0
CX02.CX12


-- ****************************************************************************
-- The relation CX20.CX21 = CX21.CX20
-- ****************************************************************************

@name drel12

CX20.CX21
r17_d → 0
elim_CX21 → 3
elim_CX21 ← 0
r17_d_dag ← 1
CX21.CX20


-- ****************************************************************************
-- The relation CX21.CCX2.CX21 = CX12.CCX1.CX12
-- ****************************************************************************

@name drel13

CX21.CCX2.CX21
r8_b → 0
r40_a → 0
r8_f ← 0
CX12.CCX1.CX12


-- ****************************************************************************
-- The relation CZ01.CCX1.CCX0.CCX1 = CCX1.CCX0.CCX1.CZ01
-- ****************************************************************************

@name drel14

CZ01.CCX1.CCX0.CCX1
r26_e → 0
r26_b → 2
r26_d → 4

-- CCX1.CZ01.CCX0.CZ02.CCX1.CZ12.CZ02
r26_c → 1
r31_b ← 6
!apply drel37 → 4

-- CCX1.CCX0.CZ01.CZ12.CCX1.CZ12
r26_d → 3
r26_e → 2
r31_b → 4

-- CCX1.CCX0.CCX1.CZ01.CZ12.CZ02.CZ02.CZ12
!apply elim_CZ02 → 5
!apply elim_CZ12 → 4
CCX1.CCX0.CCX1.CZ01


-- ****************************************************************************
-- The relation CZ12.CCX2.CCX1.CCX2 = CCX2.CCX1.CCX2.CZ12
-- ****************************************************************************

@name drel15

CZ12.CCX2.CCX1.CCX2
r8_f ← 1
!apply elim_CZ12 ← 2
r29_c ← 0

-- Z1.CX12.CZ12.CCX1.CX12
r26_d → 2
r27_b ← 0
!apply drel21 → 1

-- CX12.CCX1.CZ02.Z1.CZ12.CZ02.CX12
bifunct_Z_CZ_a → 2
r31_b → 3
!apply elim_CZ02 → 4

-- CX12.CCX1.Z1.CZ12.CX12
!apply elim_CZ12 ← 5
r29_c ← 3
!apply elim_Z1 → 2
r8_f → 0
CCX2.CCX1.CCX2.CZ12


-- ****************************************************************************
-- The relation CCX0.CCX1.CCX2.CCX1.CCX0 = CCX1.CCX2.CCX1
-- ****************************************************************************

@name drel16

CCX0.CCX1.CCX2.CCX1.CCX0
r40_a → 1
!apply drel17 → 0
r40_a ← 0
CCX1.CCX2.CCX1


-- ****************************************************************************
-- The relation CCX0.CCX2.CCX1.CCX2.CCX0 = CCX2.CCX1.CCX2
-- ****************************************************************************

@name drel17

CCX0.CCX2.CCX1.CCX2.CCX0
r8_f ← 1
elim_CX12 ← 5
elim_CX12 ← 0

-- CX12.CX12.CCX0.CX12.CCX1.CX12.CCX0.CX12.CX12
r10_b → 5
r1_b → 5
r10_b → 1

-- CX12.CCX0.CX10.CCX1.CX10.CCX0.CX12
r8_d → 2
elim_CCX0 → 4
elim_CCX0 → 1
r8_f → 0
CCX2.CCX1.CCX2


-- ****************************************************************************
-- The relation CCX1.CCX0.CCX2.CCX0.CCX1 = CCX0.CCX2.CCX0
-- ****************************************************************************

@name drel18

CCX1.CCX0.CCX2.CCX0.CCX1
r8_e ← 1
!apply elim_CX20 ← 5
!apply elim_CX20 ← 0

-- CX20.CX20.CCX1.CX20.CCX2.CX20.CCX1.CX20.CX20
r10_c → 5
r1_d → 5
r10_c → 1

-- CX20.CCX1.CX21.CCX2.CX21.CCX1.CX20
r8_b → 2
!apply elim_CCX1 → 4
!apply elim_CCX1 → 1
r8_e → 0
CCX0.CCX2.CCX0


-- ****************************************************************************
-- The relation Z2.CX12 = CX12.Z1.Z2
-- ****************************************************************************

@name drel19

Z2.CX12
elim_K12 ← 0
r25_b ← 1
!apply drel30 ← 2

-- K12.X2.CX21.K12
r12_e → 1

-- K12.CX21.X2.X1.K12
r34 → 0
r25_b_dag → 1
r25_a_dag → 2
elim_K12 → 3
bifunct_Z_c ← 1
CX12.Z1.Z2


-- ****************************************************************************
-- The relation CX12.Z2 = Z2.Z1.CX12
-- ****************************************************************************

@name drel20

CX12.Z2
elim_CX12 ← 2
!apply elim_Z1 ← 3
!apply elim_Z2 ← 4

-- CX12.Z2.CX12.Z1.Z2.Z2.Z1.CX12
!apply drel19 ← 2
!apply elim_Z2 → 1
elim_CX12 → 0
Z2.Z1.CX12


-- ****************************************************************************
-- The relation Z1.CCX1 = CCX1.CZ02.Z1
-- ****************************************************************************

@name drel21

Z1.CCX1
elim_K12 ← 0
r25_a ← 1
!apply drel29 ← 2

-- K12.X1.CCX2.K12
r5_d → 1

-- K12.CCX2.CX02.X1.K12
r25_a → 3
r35_a ← 2
!apply drel29 → 1
elim_K12 → 0
CCX1.CZ02.Z1


-- ****************************************************************************
-- The relation CCX1.Z1 = Z1.CZ02.CCX1
-- ****************************************************************************

@name drel22

CCX1.Z1
!apply elim_CCX1 ← 2
!apply elim_CZ02 ← 3
!apply elim_Z1 ← 4

-- CCX1.Z1.CCX1.CZ02.Z1.Z1.CZ02.CCX01
!apply drel21 ← 2

-- CCX1.Z1.Z1.CCX1.Z1.CZ02.CCX1
!apply elim_Z1 → 1
!apply elim_CCX1 → 0
Z1.CZ02.CCX1


-- ****************************************************************************
-- The relation Z0.CCX0 = CCX0.CZ12.Z0
-- ****************************************************************************

@name drel23

Z0.CCX0
!apply elim_SWAP01 ← 2
!apply elim_SWAP01 ← 1
!apply elim_SWAP01 ← 0

-- SWAP01.SWAP01.Z0.SWAP01.SWAP01.CCX0.SWAP01.SWAP01
symmetry_Z_a ← 1
symmetry_CCX_a ← 2

-- SWAP01.Z1.CCX1.SWAP01
!apply drel21 → 1

-- SWAP01.CCX1.CZ02.Z1.SWAP01
symmetry_CCX_a → 1
!apply elim_SWAP01 → 0

-- CCX0.SWAP01.CZ02.Z1.SWAP01
symmetry_Z_a → 3
!apply elim_SWAP01 → 5

-- CCX0.SWAP01.CZ02.SWAP01.Z0
symmetry_CZ_b ← 1
CCX0.CZ12.Z0


-- ****************************************************************************
-- The relation CCX0.Z0 = Z0.CZ12.CCX0
-- ****************************************************************************

@name drel24

CCX0.Z0
elim_CCX0 ← 2
!apply elim_CZ12 ← 3
elim_Z0 ← 4

-- CCX0.Z0.CCX0.CZ12.Z0.Z0.CZ12.CCX0
!apply drel23 ← 2

-- CCX0.Z0.Z0.CCX0.Z0.CZ12.CCX0
elim_Z0 → 1
elim_CCX0 → 0
Z0.CZ12.CCX0


-- ****************************************************************************
-- The relation X1.CCX1 = CCX1.X1
-- ****************************************************************************

@name drel25

X1.CCX1
symmetry_CCX_a → 1
symmetry_X_a → 0

-- SWAP01.X0.SWAP01.SWAP01.CCX0.SWAP01
!apply elim_SWAP01 → 2
r13 → 1
!apply elim_SWAP01 ← 2

-- SWAP01.CCX0.SWAP01.SWAP01.X0.SWAP01
symmetry_CCX_a ← 0
symmetry_X_a ← 1
CCX1.X1


-- ****************************************************************************
-- The relation X2.CCX2 = CCX1.X2
-- ****************************************************************************

@name drel26

X2.CCX2
symmetry_CCX_b → 1
symmetry_X_b → 0

-- SWAP02.X0.SWAP02.SWAP02.CCX0.SWAP02
!apply elim_SWAP02 → 2
r13 → 1
!apply elim_SWAP02 ← 2

-- SWAP02.CCX0.SWAP02.SWAP02.X0.SWAP02
symmetry_CCX_b ← 0
symmetry_X_b ← 1
CCX2.X2


-- ****************************************************************************
-- The relation CZ01.CX21.CX12.CX21 = CX21.CX12.CX21.CZ02
-- ****************************************************************************

@name drel27

CZ01.CX21.CX12.CX21
r7 ← 1
expand_SWAP12 ← 1

-- CZ01.SWAP12
!apply elim_SWAP12 ← 0
symmetry_CZ_a ← 1

-- SWAP12.CZ02
expand_SWAP12 → 0
r7 → 0
CX21.CX12.CX21.CZ02


-- ****************************************************************************
-- The relation CX21.CX12.CX21.CZ01 = CZ02.CX21.CX12.CX21
-- ****************************************************************************

@name drel28

CX21.CX12.CX21.CZ01
!apply elim_SWAP12 ← 4
!apply elim_CZ02 ← 5

-- CX21.CX12.CX21.CZ01.SWAP12.CZ02.CZ02.SWAP12
expand_SWAP12 → 4
r7 → 4
!apply drel27 ← 4

-- CX21.CX12.CX21.CZ01.CZ01.CX21.CX12.CX21.CZ02.SWAP12
elim_CZ01 → 3
elim_CX21 → 2
elim_CX12 → 1
elim_CX21 → 0

-- CZ02.SWAP12
expand_SWAP12 → 1
r7 → 1
CZ02.CX21.CX12.CX21


-- ****************************************************************************
-- The relation CCX2.K12 = K12.CCX1
-- ****************************************************************************

@name drel29

CCX2.K12
!apply elim_CCX1 ← 2
elim_K12 ← 3

-- CCX2.K12.CCX1.K12.K12.CCX1
r32 ← 2

-- CCX2.K12.K12.CCX2.K12.CCX1
elim_K12 → 1
!apply elim_CCX2 → 0
K12.CCX1


-- ****************************************************************************
-- The relation CX21.K12 = K12.CX12
-- ****************************************************************************

@name drel30

CX21.K12
elim_CX12 ← 2
elim_K12 ← 3

-- CX21.K12.CX12.K12.K12.CX12
r34 ← 2

-- CX21.K12.K12.CX21.K12.CX12
elim_K12 → 1
elim_CX21 → 0
K12.CX12


-- ****************************************************************************
-- The relation X1.Z1 = M.Z1.X1
-- ****************************************************************************

@name drel31

X1.Z1
!apply elim_X1 ← 2
!apply elim_Z1 ← 3
expand_M_1 ← 0
M.Z1.X1


-- ****************************************************************************
-- The relation X0.Z0 = M.Z0.X0
-- ****************************************************************************

@name drel32

X0.Z0
elim_X0 ← 2
elim_Z0 ← 3
expand_M_0 ← 0
M.Z0.X0


-- ****************************************************************************
-- The relation X2.Z2 = M.Z2.X2
-- ****************************************************************************

@name drel33

X2.Z2
!apply elim_X2 ← 2
!apply elim_Z2 ← 3
expand_M_2 ← 0
M.Z2.X2


-- ****************************************************************************
-- The relation Z1.X1 = X1.Z1.M
-- ****************************************************************************

@name drel34

Z1.X1
!apply elim_M ← 2
!apply elim_Z1 ← 3
!apply elim_X1 ← 4

-- Z1.X1.M.Z1.X1.X1.Z1.M
!apply drel31 ← 2

-- Z1.X1.X1.Z1.X1.Z1.M
!apply elim_X1 → 1
!apply elim_Z1 → 0
X1.Z1.M


-- ****************************************************************************
-- The relation Z2.X2 = X2.Z2.M
-- ****************************************************************************

@name drel35

Z2.X2
!apply elim_M ← 2
!apply elim_Z2 ← 3
!apply elim_X2 ← 4

-- Z2.X2.M.Z2.X2.X2.Z2.M
!apply drel33 ← 2

-- Z2.X2.X2.Z2.X2.Z2.M
!apply elim_X2 → 1
!apply elim_Z2 → 0
X2.Z2.M


-- ****************************************************************************
-- The relation CZ01.CCX2.CZ01 = CCX2
-- ****************************************************************************

@name drel36

CZ01.CCX2.CZ01
elim_K12 ← 0
r35_b → 1
r32 → 2
r35_b → 3

-- K12.CX01.CCX1.CX01.K12
r1_c → 2
elim_CX01 → 1

-- K12.CCX1.K12
r32 ← 1
elim_K12 → 0
CCX2


-- ****************************************************************************
-- The relation CZ02.CCX1.CZ02 = CCX1
-- ****************************************************************************

@name drel37

CZ02.CCX1.CZ02
elim_K12 ← 2
r35_a → 3
r32 ← 1
r35_a_dag → 0

-- K12.CX02.CCX2.CX02.K12
r1_a → 2
!apply elim_CX02 → 1

-- K12.CCX2.K12
r32 → 0
elim_K12 → 1
CCX1


-- ****************************************************************************
-- The relation CZ12.CCX0.CZ12 = CCX0
-- ****************************************************************************

@name drel38

CZ12.CCX0.CZ12
symmetry_CZ_b → 2
symmetry_CZ_b → 0
symmetry_CCX_a ← 2

-- SWAP01.CZ02.CCX1.CZ02.SWAP01
!apply drel37 → 1

-- SWAP01.CCX1.SWAP01
symmetry_CCX_a → 1
!apply elim_SWAP01 → 3
!apply elim_SWAP01 → 0
CCX0


-- ****************************************************************************
-- The relation Z2.CCX1.Z2 = CCX1
-- ****************************************************************************

@name drel39

Z2.CCX1.Z2
elim_K12 ← 0
r25_b ← 1

-- K12.X2.K12.CCX1.Z2
elim_K12 ← 4
r32 ← 3
elim_K12 → 2

-- K12.X2.CCX2.K12.Z2
r25_b ← 3

-- K12.X2.CCX2.X2.K12
!apply drel26 → 1
!apply elim_X2 → 2

-- K12.CCX2.K12
r32 → 0
elim_K12 → 1
CCX1
